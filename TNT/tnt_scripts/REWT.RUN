macro= ;
if ( argnumber )
  if ( (!eqstring[%1 ri ]) && (!eqstring[%1 RI ])
     && (!eqstring[%1 RC ]) && (!eqstring[%1 rc ])
     && (!eqstring[%1 FIT ]) && (!eqstring[%1 fit ]) )
     errmsg . 
     Valid options are: RI, RC, or FIT (default is CI).
     Option FIT allows specifying concavity (default=3).;
  end
  set 4 3 ; 
  if ( ( eqstring[%1 FIT ] || eqstring[%1 fit ] )
        && ( argnumber > 1 ) ) set 4 ( %2 ) ; end
  end 

set 2 0 ;
report- ;
silent = con buffer ; 
loop 0 nchar
   progress #1 nchar Reweighting ; 
   if ( !isact[#1] ) continue ; end
   if ( !isinfo[#1] ) continue ; end 
   set 0 10000 ;
   macfloat 5 ; 
   loop 0 ntrees
      set 1 length[#2 #1] / weight[#1] ;
      if ( '1' < '0' )
         set 0 '1' ;
         if ( ! '0' ) endloop ; end 
         end
      stop
   if ( argnumber == 0 )
        set 1 minsteps [#1] / '0' ;
   else
     if ( eqstring [ %1 ri ] || eqstring [ %1 RI ] )
          set 1  ( maxsteps[#1] - '0' ) / ( maxsteps[#1] - minsteps[#1] ) ;
     else
       if ( eqstring [ %1 rc ] || eqstring [ %1 RC ] )
           set 1  ( minsteps[#1] / '0' ) * ( ( maxsteps[#1] - '0' ) / ( maxsteps[#1] - minsteps[#1] ) ) ;
       else
           set 1  '4' / ( '4' + ( '0' - minsteps[#1] ) ) ;
       end
     end
   end
   set 0 weight[#1] ;
   set 1 '1' * 100 ;
   if ( !'1' ) set 1 1 ; end 
   macfloat 0 ; 
   if ( '0' != '1' ) set 2 ++ ; end
   ccode /'1' #1 ;
   stop
progress/ ;
lquote= ;
silent - con buffer ; 
if ( !argnumber )
quote Characters reweighted (with CI) - Changed weights: ;
else
if ( eqstring[ %1 fit ] || eqstring[%1 FIT ] )
quote Characters reweighted (with %1, conc '4') - Changed weights: ;
else 
quote Characters reweighted (with %1) - Changed weights: ;
end 
end
lquote - ;
if ( '2' ) quote yes.;
else quote no.;
end ;
report= ; 
proc/ ;









      






