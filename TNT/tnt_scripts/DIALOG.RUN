macro= ;

 /****************************************************\
|                                                      |
|   This file contains instructions to determine       |
|   a traditional search.  The dialog defined here     |
|   with opendlg has roughly the same functionality    |
|   as the traditional search dialog of TNT, with      |
|   the exception that the enforce option is available |
|   even when no constraints have been defined.        |
|   Once the dialog closes, the commands corresponding |
|   to the requested settings are invoked.             |
|   Note subordination of controls; for example, the   |
|   "none" option for swapping is only possible when   |
|   search uses wagner trees as starting points (if    |
|   swapping from trees in RAM, it makes no sense to   |
|   use as swapping algorithm "none").                 |
|                                                      |
 \****************************************************/


if ( ntax < 0 )
   quote * Cannot run traditional searches without data! ;
   errmsg ; 
   end

var =
  + searchtype
  + ranseed
  + numreps
  + fillonly
  + swaptype
  + hold
  + enforce
  + replace
  + collapsit
  + keepall
  + newhold ;

if ( ntrees < 0 ) set searchtype 0 ; end 
set swaptype 2 ;
set searchtype 0 ;
set fillonly 0 ;
set numreps 10 ;
set hold 10 ;
set enforce 0 ;
set replace 0 ;
set collapsit 0 ;
set keepall 0 ; 


/****  The opendlg command creates the dialog
       and writes values to specified variables      ********/

opendlg 50 50 450 360 Search trees... ;
    frame 10 15 420 110 Starting trees... ;
    choose searchtype
           25 35 100 18 Wagner tree(s)
           +
             spin 0 32767 ranseed 40 65 100 18 Random seed ;
             spin 1 32767 numreps 40 90 150 18 Number of repls. ;
             showtxt 270 150 100 40 Trees to save per replication ; 
             spin 1 32767 hold 270 190 70 18 ; 
             check keepall 250 250 150 18 keep all trees found ; 
             check [ ( ntrees >= 0 ) ] replace 30 290 150 18 replace existing trees ; 
           =
           [ ( ntrees >= 0 ) ] 210 35 180 16 Trees from RAM... ; 
              +
               check fillonly 235 48 180 40 Stop when maxtrees hit ;
              =
     frame 10 135 420 100 Swapping algorithm... ;
     choose swaptype
           [ +1 ] 40 160 70 18 None 
           40 180 70 18 SPR
           40 200 70 18 TBR ; 
     check collapsit 30 250 180 18 collapse trees after search ; 
     check enforce  30 270 150 18 enforce constraints ; 
closedlg ;

if ( !exstatus ) proc/; end

/*  Settings ...     */
if ( 'searchtype' == 0 && 'swaptype' == 0 ) set hold 1 ; end 
watch= ; 
rseed 'ranseed' ;
mu: rep 'numreps' hold 'hold' ;
if ( 'swaptype' == 0 ) mu: wagner ;
else
    if ( 'swaptype' == 1 ) mu: spr ; bb: spr ;
    else
        if ( 'swaptype' == 2 ) mu: tbr ; bb: tbr ;
        end
    end
end
if ( 'keepall' ) mu: keep ; else mu: nokeep ; end
if ( 'fillonly' ) bb:fill ; else bb:nofill; end
if ( 'enforce' ) constrain= ; else constrain- ; end
if ( 'replace' && ( 'searchtype'== 0 ) ) keep 0 ; end
if ( 'collapsit' ) collapse auto ; else collapse noau ; end 

if ( 'searchtype == 0 ) 
   set newhold ( ntrees+1 ) + ( 'hold' * 'numreps' ) ;
   hold 'newhold' ;
   end 

/*  Do the search...       */
if ( 'searchtype' ) bb ; else mu ; end ;

proc/ ;

        
